/*-------------------------------------------------------------------*/
/* Program: HUM010CL                                                 */
/* Description: Driver CL for Humana export process for 590-001-0043 */
/*              (MacFadden)                                          */
/* Author: Debora Bond                                               */
/* Date: 08/14/2013                                                  */
/*                                                                   */
/*-------------------------------------------------------------------*/
/* Modifications:                                                    */
/* SSK 09/19/2013 ADD AUTO ARCHIVE FOR DATA FILES                    */
/*   test                                                            */
/*                                                                   */
/*                                                                   */
/*                                                                   */
/*                                                                   */
/*-------------------------------------------------------------------*/
             PGM

             DCL        VAR(&EXPORTID) TYPE(*CHAR) LEN(10) +
                          VALUE('0000000093')
             DCL        VAR(&ERROR_MSG) TYPE(*CHAR) LEN(90)
             DCL        VAR(&EMAIL_TXT) TYPE(*CHAR) LEN(256) +
                          VALUE('The Humana Enrollment Export job +
                          encountered the following error:  ')

/* Clear Humana files:  file HUM010WF is the actual flat file that  */
/* will be transmitted.  The other files are written to only to aid */
/* the programmer in seeing the actual data in their respective     */
/* formats and fields.                                              */
             CLRPFM     FILE(HUM010WF)
             CLRPFM     FILE(HUMDETAIL)
             CLRPFM     FILE(HUMTRAILR)

             OVRDBF     FILE(MEMAC#1) TOFILE(QS36F/F.MEMAC#1)
             OVRDBF     FILE(DEPNDC) TOFILE(QS36F/F.DEPNDC)

             CALL       PGM(HUM010R) PARM(&EXPORTID &ERROR_MSG)

/* If Export program encountered any errors - send email to Ops */
             IF         COND(&ERROR_MSG *NE ' ') THEN(DO)
             CHGVAR     VAR(&EMAIL_TXT) VALUE(&EMAIL_TXT *BCAT ' ' +
                          *CAT &ERROR_MSG)
             EMAIL      SUBJECT('HUMANA EXPORT ERROR!!!') +
                          MSGTXT(&EMAIL_TXT) EMAIL(DBOND@GBSIO.NET)
             EMAIL      SUBJECT('HUMANA EXPORT ERROR!!!') +
                          MSGTXT(&EMAIL_TXT) +
                          EMAIL(AS400_OPERATORS@GBSIO.NET)
             ENDDO

/* FTP SCRIPT      */

             CHKOBJ     OBJ(QTEMP/IOFTPSC0) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CRTPF +
                          FILE(QTEMP/IOFTPSC0) +
                          SRCFILE(GBSPGM/QDDSSRC) OPTION(*NOSRC +
                          *NOLIST))

             CLRPFM     FILE(QTEMP/IOFTPSC0)

             OVRDBF     FILE(IOFTPSC0) TOFILE(QTEMP/IOFTPSC0)

             CALL       PGM(HUM001R)

             DLTOVR     FILE(*ALL)

             CPYF       FROMFILE(QTEMP/IOFTPSC0) +
                          TOFILE(GBSPGM/QTXTSRC) TOMBR(CMFHUMXPRT) +
                          MBROPT(*REPLACE) FMTOPT(*CVTSRC)

/* send files to ftp server                                               */
             CLRPFM     FILE(GBSPGM/QTXTSRC) MBR(LOGHUMAPRT)

             OVRDBF     FILE(INPUT) TOFILE(GBSPGM/QTXTSRC) +
                          MBR(CMFHUMXPRT)

             OVRDBF     FILE(OUTPUT) TOFILE(GBSPGM/QTXTSRC) +
                          MBR(LOGHUMAPRT)

             FTP        RMTSYS(LOOPBACK)

             DLTOVR     FILE(INPUT OUTPUT)

             OVRPRTF    FILE(QSYSPRT) OUTQ(GALIB/GAOUTQ) HOLD(*YES)
             CPYF       FROMFILE(GBSPGM/QTXTSRC) TOFILE(*PRINT) +
                          FROMMBR(LOGHUMAPRT)
             DLTOVR     FILE(*ALL)

             RCLRSC

 /*          EMAIL      SUBJECT('Humana Export For MacFadden') +      */
 /*                       MSGTXT('Export file for MacFadden (acct#: + */
 /*                       590-001-0043) is ready to be sent to +      */
 /*                       carrier.  File is under sub-directory +     */
 /*                       HUMANA_FTP.') EMAIL(DBOND@GBSIO.NET)        */

             EMAIL      SUBJECT('Humana Export For MacFadden') +
                          MSGTXT('Export file for MacFadden (acct#: +
                          590-001-0043) is ready to be sent to +
                          carrier.  File is under sub-directory +
                          HUMANA_FTP.') +
                          EMAIL(jtorres@@GBSIO.NET)

             ENDPGM
